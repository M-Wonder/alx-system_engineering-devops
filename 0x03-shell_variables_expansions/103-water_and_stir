#!/bin/bash
awk '
function val(ch, s,    i) {
  for (i = 1; i <= length(s); i++)
    if (substr(s, i, 1) == ch)
      return i - 1
  return -1
}

function todec(str, s,    i, base, d, dec) {
  base = length(s)
  dec = 0
  for (i = 1; i <= length(str); i++) {
    d = val(substr(str, i, 1), s)
    dec = dec * base + d
  }
  return dec
}

function fromdec(n, s,    res, base, r, ch) {
  base = length(s)
  if (n == 0)
    return substr(s, 1, 1)
  res = ""
  while (n > 0) {
    r = n % base
    n = int(n / base)
    ch = substr(s, r + 1, 1)
    res = ch res
  }
  return res
}

BEGIN {
  from1 = "water"
  from2 = "stir."
  to = "bestchol"

  a = ENVIRON["WATER"]
  b = ENVIRON["STIR"]

  if (a == "") a = "0"
  if (b == "") b = "0"

  dec = todec(a, from1) + todec(b, from2)
  print fromdec(dec, to)
}'
